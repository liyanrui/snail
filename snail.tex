\startluacode
local 蜗界 = {}
蜗界.字号 = "BodyFontSize" -- ConTeXt 正文字体所用字号
蜗界 = {
    字号 = 蜗界.字号,
    文字 = {颜色 = "black"},
    框形 = "fullsquare",
    框 = {余地 = 蜗界.字号, 
          线宽 = ".175" .. 蜗界.字号, 
          颜色 = "darkgray",
          各向同性 = "false",
          郊 = "1.5" .. 蜗界.字号},
    背景 = {颜色 = "lightgray"},
    路径 = {线宽 = ".2" .. 蜗界.字号,
            颜色 = "darkgray",
            圆角 = ".25" .. 蜗界.字号},
    玩笑 = "0"
}
table.save("snail.conf", 蜗界)
\stopluacode

\startMPinclusions
lua("蜗界 = table.load('snail.conf')");
def 获 expr a = lua("mp.print(" & a & ")") enddef;
def 设 expr a = lua(a) enddef;
def 恢复默认蜗界 =
  lua("蜗界 = table.load('snail.conf')")
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 显 = true enddef;
def 隐 = false enddef;
def 中央四角十二门 (suffix foo) (expr 或显或隐) = 
  forsuffixes i = 中央, 东北角, 东南角, 西南角, 西北角,
                  子门, 卯门, 午门, 酉门,
                  丑门, 寅门, 辰门, 巳门, 未门, 申门, 戌门, 亥门:
    pair foo.i;
  endfor;
  foo.中央 := (center foo);
  foo.西北角 := (ulcorner foo); 
  foo.东北角 := (urcorner foo);
  foo.东南角 := (lrcorner foo);
  foo.西南角 := (llcorner foo);
  foo.子门 := .5[foo.西北角, foo.东北角];
  foo.午门 := .5[foo.西南角, foo.东南角];
  foo.卯门 := .5[foo.东南角, foo.东北角];
  foo.酉门 := .5[foo.西南角, foo.西北角];
  foo.丑门 := .5[foo.子门, foo.东北角];
  foo.寅门 := .5[foo.卯门, foo.东北角];
  foo.辰门 := .5[foo.卯门, foo.东南角];
  foo.巳门 := .5[foo.午门, foo.东南角];
  foo.未门 := .5[foo.午门, foo.西南角];
  foo.申门 := .5[foo.酉门, foo.西南角];
  foo.戌门 := .5[foo.酉门, foo.西北角];
  foo.亥门 := .5[foo.子门, foo.西北角];
  if 或显或隐:
    forsuffixes i = 东北角, 东南角, 西南角, 西北角:
      draw foo.i withpen pensquare scaled 4pt withcolor darkblue;
    endfor;
    forsuffixes i = 子门, 卯门, 午门, 酉门:
      draw foo.i withpen pencircle scaled 4pt withcolor darkred;
    endfor;
    forsuffixes i = 丑门, 寅门, 辰门, 巳门, 未门, 申门, 戌门, 亥门:
      draw foo.i
        withpen pencircle scaled 3pt withcolor darkgreen;
    endfor;
  fi;
enddef;
def 郊 (suffix foo) (expr 或显或隐) = 
  forsuffixes i = 乾, 坤, 艮, 兑, 巽, 震, 坎, 离:
    pair foo.i;
  endfor;
  begingroup
    save outskirts; path outskirts;
  outskirts := foo enlarged (获 "蜗界.框.郊");
  foo.艮 := (ulcorner outskirts); 
  foo.震 := (urcorner outskirts);
  foo.兑 := (lrcorner outskirts);
  foo.巽 := (llcorner outskirts);
  foo.坤 := .5[foo.艮, foo.震];
  foo.乾 := .5[foo.巽, foo.兑];
  foo.离 := .5[foo.兑, foo.震];
  foo.坎 := .5[foo.巽, foo.艮];
  if 或显或隐:
    forsuffixes i = 乾, 坤, 艮, 兑, 巽, 震, 坎, 离:
      draw foo.i withpen pensquare scaled 4pt withcolor magenta;
    endfor;
  fi;
enddef;
def 之中央 = ("中央") enddef;
def 之酉门 = ("酉门") enddef;
\stopMPinclusions

\startMPinclusions[+]
def 原点 = (0, 0) enddef;
def 偏 = shifted enddef;
tertiarydef a 位于 b =
  (if pair b: b else: center b fi - if pair a: a else: center a fi)
enddef;
def 定位 (suffix name) (expr 多少呢) =
  name := name 偏 (多少呢);
  中央四角十二门(name, 隐); 郊(name, 隐);
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 宫 (suffix name) (expr a) text 多少呢 =
  picture name;
  begingroup
  save 框, 文, w, h, s, delta;
  path 框; picture 文; numeric w, h, s; pair delta;
  文 := textext(a);
  w := bbwidth 文; h := bbheight 文;
  if (获 "蜗界.框.各向同性"):
    if w > h: s = w; else: s = h; fi;
    框 := fullsquare xysized (s, s) enlarged ((获 "蜗界.框.余地") * (1, 1));
  else:
    框 := fullsquare xysized (w, h) enlarged ((获 "蜗界.框.余地") * (1, 1));
  fi;
  框 := (获 "蜗界.框形") xysized (bbwidth 框, bbheight 框);
  if (获 "蜗界.玩笑") > 0: 框 := 框 randomized (获 "蜗界.玩笑"); fi;
  name := image (fill 框 withcolor (获 "蜗界.背景.颜色");
                 draw 框 withpen pencircle scaled (获 "蜗界.框.线宽")
                         withcolor (获 "蜗界.框.颜色");
                 draw 文 withcolor (获 "蜗界.文字.颜色"););
  delta := (0, 0) 多少呢;
  name := name 偏 delta;
  中央四角十二门(name, 隐); 郊(name, 隐);
  endgroup;
enddef;
\stopMPinclusions

\defineframed[snailframed][offset=1em,frame=off]
\startMPinclusions[+]
def 廷 (suffix name) (expr a) text 之所在 =
  picture name;
  begingroup
    save 文; picture 文; 文 := textext("\snailframed{" & a & "}");
    name := image (draw 文 withcolor (获 "蜗界.文字.颜色"););
    name := name 之所在; 四角十二门(name, 隐); 郊(name, 隐);
  endgroup;
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 宫廷 (suffix a) (expr b) = picture a; a := b; enddef;
def 城 (suffix a) (text b) =
  宫廷(a, nullpicture);
  for i = b: addto a also image(draw i); endfor;
  四角十二门(a, 隐); 郊(a, 隐);
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 景物 (suffix name) (expr a, w, h) text 之所在 =
  picture name;
  begingroup;
  save p, s; picture p; numeric s;
  p := externalfigure a;
  s := (bbwidth p) / (bbheight p);
  if numeric w and numeric h:
    name := image(draw externalfigure a xysized (w, h););
  else:
    if numeric w:
      name := image(draw externalfigure a xysized (w, w / s););
    fi;
    if numeric h:
       name := image(draw externalfigure a xysized (h * s, h););
    fi;
  fi;
  name := name 之所在;
  draw name;  
  endgroup;
enddef;
\stopMPinclusions

\startMPinclusions[+]
pair 竖亥;
tertiarydef a 经转 b =
  hide(竖亥 := (xpart (if path a: point (length a) of a else: a fi), ypart (b)))
  a -- 竖亥 -- b
enddef;
tertiarydef a 纬转 b =
  hide(竖亥 := (xpart (b), ypart (if path a: point (length a) of a else: a fi)))
  a --  竖亥 -- b
enddef;
\stopMPinclusions

\startMPinclusions[+]
vardef 经距 (expr a, b) = (abs(xpart (a) - xpart (b))) enddef;
vardef 纬距 (expr a, b) = (abs(ypart (a) - ypart (b))) enddef;
\stopMPinclusions

\startMPinclusions[+]
tertiarydef a => b =
  begingroup
    save outgoing, incoming, va, vb, do_nothing;
    pair outgoing, incoming, va[], vb[];
    boolean do_nothing; do_nothing := false;
    va[1] := if pair a: a else: llcorner a fi;
    va[2] := if pair a: a else: urcorner a fi;
    vb[1] := if pair b: b else: llcorner b fi;
    vb[2] := if pair b: b else: urcorner b fi;
    if xpart va[2] < xpart vb[1]: % a 在 b 的左侧
      outgoing := if pair a: a else: 0.5[lrcorner a, urcorner a] fi;
      incoming  := (xpart vb[1], ypart outgoing);
    elseif xpart va[1] > xpart vb[2]: % a 在 b 的右侧
      outgoing := if pair a: a else: 0.5[llcorner a, ulcorner a] fi;
      incoming  := (xpart vb[2], ypart outgoing);
    elseif ypart va[1] > ypart vb[2]: % a 在 b 的上方
      outgoing := if pair a: a else: 0.5[llcorner a, lrcorner a] fi;
      incoming  := (xpart outgoing, ypart vb[2]);
    elseif ypart va[2] < ypart vb[1]: % a 在 b 的下方
      outgoing := if pair a: a else: 0.5[ulcorner a, urcorner a] fi;
      incoming  := (xpart outgoing, ypart vb[1]);
    else:
      do_nothing := true;
    fi;
    if do_nothing: nullpicture else: outgoing -- incoming fi
  endgroup
enddef;

drawpathoptions(withpen pencircle scaled (获 "蜗界.路径.线宽") withcolor (获 "蜗界.路径.颜色"));
def 流向 text a =
  drawarrowpath if (获 "蜗界.玩笑") > 0: (a) randomized (获 "蜗界.玩笑") else: a fi;
enddef;
def 串联 text a =
  drawpath if (获 "蜗界.玩笑") > 0: (a) randomized (获 "蜗界.玩笑") else: a fi;  
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 开启虚线模式 =
  drawpathoptions(dashed (evenly scaled .625(获 "蜗界.路径.线宽"))
                  withpen pencircle scaled (获 "蜗界.路径.线宽")
                  withcolor (获 "蜗界.路径.颜色"));
enddef;
def 关闭虚线模式 =
  drawpathoptions(withpen pencircle scaled (获 "蜗界.路径.线宽")
                  withcolor (获 "蜗界.路径.颜色"));
enddef;
% 默认关闭虚线模式
关闭虚线模式;
\stopMPinclusions

\startMPinclusions[+]
def 北 = up * enddef; def 南 = down * enddef;
def 西 = left * enddef; def 东 = right * enddef;
def 北行 expr a = (北 (a)) enddef;
def 南行 expr a = (南 (a)) enddef;
def 西行 expr a = (西 (a)) enddef;
def 东行 expr a = (东 (a)) enddef;

def 从 = enddef;
tertiarydef a 向 b =
  if pair a:
    a -- (a shifted b)
  elseif path a:
    a -- ((point (length a) of a) shifted (b))
  fi
enddef;
tertiarydef a 纬至 b = 
  if pair a:
    a -- (a shifted (0, ypart b - ypart a))
  elseif path a:
    hide(竖亥 := (point (length a) of a))
    a -- (竖亥 shifted (0, ypart b - ypart 竖亥))
  fi
enddef;
tertiarydef a 经至 b = 
  if pair a:
    a -- (a shifted (xpart b, 0))
  elseif path a:
    hide(竖亥 := (point (length a) of a))
    a -- (竖亥 shifted (xpart b - xpart 竖亥, 0))
  fi
enddef;
\stopMPinclusions

\startMPinclusions[+]
vardef 柔和 (expr p) =
  save n, nodes, n, new_p, 圆角; 
  numeric n, 圆角; pair nodes[]; path new_p;
  n := length p;
  for i = 0 upto n:
    nodes[i] := point i of p;
  endfor;
  new_p := nodes[0];
  圆角 := (获 "蜗界.路径.圆角");
  for i = 1 upto n - 1:
    new_p := new_p -- point -圆角 on (nodes[i - 1] -- nodes[i]);
    new_p := new_p .. point 圆角 on (nodes[i] -- nodes[i + 1]);
  endfor;
  new_p := new_p -- nodes[n];
  new_p
enddef;
\stopMPinclusions

\startMPinclusions[+]
vardef 最长一段的中点 (expr p) =
  save n, prev, current, l, l_max, s_max, t, simple_p;
  numeric n, l, l_max;
  pair prev, current; 
  path simple_p, t, s_max;
  boolean copath;
  n := length p;
  l_max := 0;
  for i = 1 upto n:
    prev := point i - 1 of p;
    current := point i of p;
    t := prev -- current;
    l := arclength t;
    if l_max < l:
      l_max := l;
      s_max := t;
    fi;
  endfor;
  point .5 along (s_max)
enddef;
def 标注之体 (expr tag, anchor, c) text p =
  begingroup
    save formatted_tag, pos, offset, t;
    pair pos; numeric offset; string t, formatted_tag;
    formatted_tag := "\tfx" & tag;
    if c < 0:
      pos := 最长一段的中点 p;
    else:
      pos := point c of p;
    fi;
    offset := .25(获 "蜗界.字号");
    if anchor = "北":
      pos := pos shifted (0, offset);
      t := "thetextext.top";
    elseif anchor = "东":
      pos := pos shifted (offset, 0);
      t := "thetextext.rt";
    elseif anchor = "南":
      pos := pos shifted (0, -offset);
      t := "thetextext.bot";
    elseif anchor = "西":
      pos := pos shifted (-offset, 0);
      t := "thetextext.lft";
    fi;
    draw scantokens(t)(formatted_tag, pos);
  endgroup;
enddef;
def 标注(expr tag, anchor) text p =
  标注之体 (tag, anchor, -1) (p);
enddef;
def 定位标注(expr tag, anchor, c) text p =
  标注之体 (tag, anchor, c) (p);
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 有向 = "有向" enddef;
def 无向 = "无向" enddef;
def 路 (suffix name) (expr dir, p) text label =
  path name; name := 柔和(p);
  if dir = "有向":
    流向 name;
  else:
    串联 name;
  fi;
  label name;
enddef;
\stopMPinclusions

\startMPinclusions[+]
def 尺寸 (suffix a) (expr b) =
  numeric a;
  a := b;
enddef;
\stopMPinclusions
